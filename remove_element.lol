HAI 1.3
    BTW MAKES A MATRIX WITH N ELEMENTS THAT ARE INPUTTED
    HOW IZ I MAKEMATRIX YR N
        I HAS A MATRIX ITZ A BUKKIT
        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX N
            I HAS A NUM1
            VISIBLE "NUMBR PLS:: "
            GIMMEH NUM1
            NUM1 IS NOW A NUMBR

            MATRIX HAS A SRS INDEX ITZ NUM1
        IM OUTTA YR LOOP

        FOUND YR MATRIX
    IF U SAY SO

    OBTW
        https://leetcode.com/problems/remove-element/description/
        Given an integer array `nums` and an integer `val`, remove all occurrences of `val` in `nums`
    TLDR

    BTW REMOVES ALL OCCURRENCES OF 'VAL' IN 'MATRIX'
    HOW IZ I REMOVEELEMENT YR NUMS AN YR N AN YR VAL
        I HAS A NEWMAT ITZ A BUKKIT
        I HAS A NEWINDEX ITZ 0

        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX N
            DIFFRINT NUMS'Z SRS INDEX AN VAL, O RLY?
                YA RLY
                NEWMAT HAS A SRS NEWINDEX ITZ NUMS'Z SRS INDEX
                NEWINDEX R SUM OF NEWINDEX AN 1
            OIC
        IM OUTTA YR LOOP

        BTW RETURNS A BUKKIT CONTAINING A SIZE AND A BUKKIT (CANT GET SIZE ME THINKS)
        I HAS A RETURNM ITZ A BUKKIT
        RETURNM HAS A SIEZ ITZ NEWINDEX
        RETURNM HAS A LIZT ITZ NEWMAT

        FOUND YR RETURNM
    IF U SAY SO

    BTW MAIN

    I HAS A N
    VISIBLE "GIMMEH BUKKIT SIEZ:: "
    GIMMEH N
    N IS NOW A NUMBR
    I HAS A MATRIX ITZ A BUKKIT
    MATRIX R I IZ MAKEMATRIX YR N MKAY BTW MAKES MATRIX (BUKKIT/ARRAY)

    BTW PRINTS THE CONTENTS OF THE ARRAY
    VISIBLE "YR BUKKIT:: "
    IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX N
        VISIBLE MATRIX'Z SRS INDEX
    IM OUTTA YR LOOP

    BTW GETS VALUE TO DELETE
    I HAS A VAL
    VISIBLE "GIMMEH VAL TO DELET:: "
    GIMMEH VAL
    VAL IS NOW A NUMBR

    I HAS A REMO ITZ A BUKKIT
    REMO R I IZ REMOVEELEMENT YR MATRIX AN YR N AN YR VAL MKAY

    MATRIX R REMO'Z LIZT BTW 

    BTW PRINTS THE CONTENTS OF THE ARRAY AFTER REMOVE
    VISIBLE "YR BUKKIT AFTA DELET:: "
    IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX AN REMO'Z SIEZ
        VISIBLE MATRIX'Z SRS INDEX
    IM OUTTA YR LOOP

KTHXBYE
